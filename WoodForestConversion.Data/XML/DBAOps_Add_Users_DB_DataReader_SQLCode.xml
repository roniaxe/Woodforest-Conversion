<?xml version="1.0"?>
<?xml-stylesheet type="text/xsl" href="DBAOps_Add_Users_DB_DataReader_SQLCode.xml"?>
<executesql>
-- turn off rowcount
set nocount on

declare
	@DatabaseName sysname,
	@SQLCmd nvarchar(max),
	@NTUser sysname

set @NTUser = 'Woodforest\SQLServer Data Readers'

-- check to see if login exists for NT User, if not, add login
if not exists(select * from master.dbo.syslogins where name = @NTUser)
begin

	-- output creating user statement
	print 'Creating login [' + @NTUser + ']'

	-- dynamically build create login statement
	set @SQLCmd = 'create login [' + @NTUser + '] from windows'

	-- execute dynamic statement
	exec (@SQLCmd)

end

-- create temp table to store user info
create table #tempUsers
(
	UserName		sysname,
	GroupName		sysname,
	LoginName		sysname null,
	DefDBName		sysname null,
	DefSchemaName	sysname null,
	UserID			int,
	SID				varbinary(85) null
)

-- declare cursor for database information
declare curDatabaseList cursor for
select
	name
from
	master.dbo.sysdatabases 
where
	name not in ('master','tempdb','msdb','model','distribution','pubs','northwind')
order by
	name

-- open cursor
open curDatabaseList

-- fetch record
fetch next from curDatabaseList into @DatabaseName

-- loop through records
while @@fetch_status = 0
begin

	-- empty temp table for each database
	truncate table #tempUsers

	-- dynamically build insert statement to determine users in database
	set @SQLCmd = 'insert #tempUsers exec (''' + 'exec [' + @DatabaseName + '].[dbo].sp_helpuser'')'

	-- execute dynamic sql statement
	exec (@SQLCmd)

	-- if user does not exist, add
	if not exists(select * from #tempUsers where UserName = @NTUser)
	begin

		-- output creating user statement
		print 'Adding user [' + @NTUser + '] to database [' + @DatabaseName + ']'

		-- build dynamic sql statement to create user		
		set @SQLCmd = 'use [' + @DatabaseName + ']; create user [' + @NTUser + '] for login [' + @NTUser + ']'

		-- execute statement
		exec (@SQLCmd)

	end

	-- if db_datareader role has not been assigned to user, add
	if not exists(select * from #tempUsers where UserName = @NTUser and GroupName = 'db_datareader')
	begin
		print 'Adding role db_datareader to User [' + @NTUser + '] in database ' + @DatabaseName

		-- build dynamic sql statement to add user role
		set @SQLCmd = 'use [' + @DatabaseName + ']; exec sp_addrolemember N''db_datareader'', N''' + @NTUser + ''''

		-- execute statement
		exec (@SQLCmd)

	end

	-- fetch record
	fetch next from curDatabaseList into @DatabaseName

END

-- close cursor
close curDatabaseList
deallocate curDatabaseList

drop table #tempUsers
</executesql>